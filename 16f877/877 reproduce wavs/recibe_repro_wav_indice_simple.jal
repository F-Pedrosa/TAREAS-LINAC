include 16f877

pragma target OSC HS
pragma target clock 20_000_000
pragma target WDT  disabled
pragma target LVP  disabled

include delay

; setup rs-232 x hardware
const serial_hw_baudrate  = 115200
include serial_hardware
serial_hw_init()

; led testigo
alias  testigo is pin_c0
pin_c0_direction = output

portb_direction = all_output

; setup spi e inclusión librería spi (el hardware para comunicarse con la SD)
include spi_master_hw
spi_init(SPI_MODE_11,SPI_RATE_FOSC_4)
; definir entradas/salidas spi (no hay mucha alternativa, las fija el hard, excepto a ChipSelect)
pin_sdi_direction = input    ; entrada datos spi
pin_sdo_direction = output   ; salida datos spi
pin_sck_direction = output   ; clock spi (salida)
; spi chip select pin
ALIAS chip_select_direction   is pin_c2_direction
ALIAS chip_select             is pin_c2
chip_select_direction = output    ; pin chip select como salida
chip_select = low                 ; arrancar con chip slect en 0

; incluir librería sd card
include sd_card
sd_init()       ; inicializar la tarjeta sd

; librería para acceso eeprom INTERNA del PIC
include pic_data_eeprom


const byte msg_inicio[] = "Iniciando programa y esperando comando..."

const byte msg_indice[] = "Contenido del Indice:"
const byte msg_indice_nombre[] = "Nombre:"
const byte msg_indice_tamanio[] = "Tamaño:"
const byte msg_indice_sector[] = "Sector inicial:"

; arreglo que contendrá el nombre del archivo, enviado desde la PC, asumimos tamaño máximo 20 caracteres.
var byte nombre_archivo[20]

; led testigo
alias led is pin_c0
pin_c0_direction = output

; Globales
var byte desde_pc = 0
var dword tamanio = 0
var dword contador = 0
var byte indice = 0
var byte individual[12]
var byte indi[4] at tamanio
var byte low_byte, high_byte
var bit bandera = 0
var byte y = 0

var byte digi5 = 0
var byte digi4 = 0
var byte digi3 = 0
var byte digi2 = 0
var byte digi1 = 0
var byte digi0 = 0

var dword sector_inicial = 2

; --------------------------------------- Procedimientos --------------------------------------------

procedure cr_lf is
   serial_hw_write (13)
   serial_hw_write (10)
end procedure

function dig2bin(byte in di2,byte in di1,byte in di0) return byte is
   var byte resultado = 0
   var byte temporal = 0

   di2 = di2 - 48
   di1 = di1 - 48
   di0 = di0 - 48

   if di2 != 0 then
      resultado = di2 * 100
   end if
   temporal = di1 * 10
   resultado = resultado + temporal
   resultado = resultado + di0
   return resultado
end function

procedure bin2digits( byte out dig2, byte out dig1, byte out dig0, byte in numero ) is
   var byte centena = 0
   var byte decena = 0
   centena = numero / 100
   dig2 = centena
   centena = dig2 * 100
   numero = numero - centena
   decena = numero / 10
   dig1 = decena
   decena = dig1 * 10
   numero = numero - decena
   dig0 = numero
end procedure

; Para convertir un word en 4 dígitos separados.
procedure word2digits( byte out dig3, byte out dig2, byte out dig1, byte out dig0, word in numero ) is

   var word miles = 0
   var byte centena = 0
   var byte decena = 0

   miles = numero / 1000
   dig3 = byte(miles)
   miles = miles * 1000
   numero = numero - miles
   centena = byte(numero / 100)
   dig2 = centena
   centena = dig2 * 100
   numero = numero - centena
   decena = byte(numero / 10)
   dig1 = decena
   decena = dig1 * 10
   numero = numero - decena
   dig0 = byte(numero)

end procedure

; Para convertir un dword en 6 dígitos separados.
procedure dword2digits( byte out dig5, byte out dig4, byte out dig3, byte out dig2, byte out dig1, byte out dig0, dword in numero ) is

   var dword centemil = 0
   var dword decemil = 0
   var dword miles = 0
   var dword centena = 0
   var dword decena = 0

   centemil = numero / 100_000
   dig5 = byte( centemil)
   centemil = centemil * 100_000
   numero = numero - centemil
   decemil = numero / 10_000
   dig4 = byte(decemil)
   decemil = decemil * 10_000
   numero = numero - decemil
   miles = numero / 1000
   dig3 = byte(miles)
   miles = miles * 1000
   numero = numero - miles
   centena = numero / 100
   dig2 = byte(centena)
   centena = dig2 * 100
   numero = numero - centena
   decena = numero / 10
   dig1 = byte(decena)
   decena = dig1 * 10
   numero = numero - decena
   dig0 = byte(numero)

end procedure

procedure msg_serial(byte in string[], bit in cr) is
   var word len = count(string)
   var byte i
   for len using i loop
      serial_hw_write(string[i])
   end loop
   if cr then
      cr_lf
   end if
end procedure


; --------------------------------------- PRINCIPAL --------------------------------------------

var byte i = 0

; lazo para evitar el chasquido de encendido, al reproducir un wav desde la SD
y = 0
portb = 0
for 128 using y loop
    portb = y
    _usec_delay(25)
end loop
; testigo luminoso
testigo = on
for 2 loop
   delay_100ms(10)
end loop
testigo = off


var byte cuenta_ceros = 0

forever loop
  if Serial_hw_read(desde_pc) then
    ; si es llegó "a" quiere decir que a continuación viene el wav en forma de stream de bytes
    ; precedido por la info (12 bytes) que indica el tamaño del archivo (como string).
    if desde_pc == "a" then
       testigo = on
       ; recibir 12 bytes con tamaño archivo
       indice = 0
       for 12 loop
           desde_pc = serial_hw_data
           individual[indice] = desde_pc
           indice = indice + 1
       end loop
       indi[3] = dig2bin(individual[0],individual[1],individual[2])
       indi[2] = dig2bin(individual[3],individual[4],individual[5])
       indi[1] = dig2bin(individual[6],individual[7],individual[8])
       indi[0] = dig2bin(individual[9],individual[10],individual[11])
       ; a partir de acá recibir el stream de bytes y escribirlos en la SD
       ; y ver si se llegó a la cantidad de bytes dada por el tamaño?
       sd_start_write(sector_inicial)                 ; arrancamos la escritura (=2 en primera corrida)
       contador = 1
       while contador != tamanio loop
          desde_pc = serial_hw_data
          if bandera == 0 then
             low_byte = desde_pc
             bandera = 1
          else
             high_byte = desde_pc
             bandera = 0
             sd_write_data(low_byte, high_byte)
          end if
          contador = contador + 1
       end loop
       sd_write_to_sector_end(0x00)
       ; escribir dos sectores completo en blanco, como separación entre wavs
       for 512 loop
           sd_write_data(0,0)
       end loop
       sd_stop_write()
       testigo = off
       ; para ir incrementando la cuenta de sectores
       sector_inicial = tamanio/512
       sector_inicial = sector_inicial + 5
    elsif desde_pc == "b" then
       testigo = on
       ; leer bytes desde la tarjeta SD y mandarlos al puerto B, donde está conectados
       ; un DAC0800 y un amplificador de audio
       sd_start_read(2)  ; alistar sd card para lectura sectores predefinidos (que debe coincidir con los escritos antes)
       contador = 0
       while contador < 400_000 loop             ; tamaño wav dividido en 2 porque leo de a 2 bytes de la SDS
          sd_read_data (low_byte, high_byte)  ; leer 2 bytes
          ; La tasa de muestreo se asume en 22050 (debe coincidir con la del wav!!!!!)
          ; retardo para adecuar la tasa de muestreo? usar interrupción?
          ; Para una tasa de muestreo de 22050, corresponde un delay de ~45 us entre muestras (?)
          ; mandar al DAC
          PORTB = low_byte
          _usec_delay(35)
          PORTB = high_byte
          _usec_delay(30)
          contador = contador + 1
       end loop
       contador = 0
       sd_stop_read()
       testigo = off
    elsif desde_pc == "1" then       ; como testigo de comunicación
       serial_hw_write ("O")
       serial_hw_write ("K")
       cr_lf
    elsif desde_pc == "2" then       ; para borrar sectores iniciales de la SD y dejarlos en 0
       testigo = on
       sd_start_write(2)
       for 500_000 loop              ; sectores x 2 bytes x 256 (sector = 512 bytes)
          sd_write_data(0,0)
       end loop
       sd_stop_write()
       testigo = off
    end if
  end if
end loop

